/-
Copyright (c) 2026 Palalansoukî. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Palalansoukî
-/

import LeanPool.Incompleteness.Foundation.Modal.Entailment.GL
import LeanPool.Incompleteness.Foundation.Modal.ComplementClosedConsistentFinset
import LeanPool.Incompleteness.Foundation.Modal.Kripke.Hilbert.GL.Soundness

/-! # Completeness -/


namespace LO
namespace Modal
namespace Hilbert
namespace GL
namespace Kripke

open _root_.LO.Modal.Kripke
open Entailment
open Formula
open Entailment Entailment.FiniteContext
open Formula.Kripke
open ComplementClosedConsistentFinset

variable {φ ψ : Formula ℕ}

/-- Imported declaration from the Incompleteness formalization. -/
abbrev miniCanonicalFrame (φ : Formula ℕ) : Kripke.FiniteFrame where
  World := ComplementClosedConsistentFinset Hilbert.GL φ.subformulas
  Rel X Y :=
    (∀ ψ ∈ □''⁻¹φ.subformulas, □ψ ∈ X → (ψ ∈ Y ∧ □ψ ∈ Y)) ∧
    (∃ χ ∈ □''⁻¹φ.subformulas, □χ ∉ X ∧ □χ ∈ Y)

namespace miniCanonicalFrame

lemma is_irreflexive : Std.Irrefl (miniCanonicalFrame φ).Rel := by
  constructor
  simp_all

lemma is_transitive : IsTrans (miniCanonicalFrame φ).World (miniCanonicalFrame φ).Rel := by
  constructor
  rintro X Y Z ⟨RXY, ⟨χ, _, _, _⟩⟩ ⟨RYZ, _⟩;
  constructor;
  · simp_all
  · use χ;
    simp_all

end miniCanonicalFrame


/-- Imported declaration from the Incompleteness formalization. -/
abbrev miniCanonicalModel (φ : Formula ℕ) : Kripke.Model where
  toFrame := miniCanonicalFrame φ |>.toFrame
  Val X a := (atom a) ∈ X


lemma truthlemma_lemma1
  {X : ComplementClosedConsistentFinset Hilbert.GL φ.subformulas} (hq : □ψ ∈ φ.subformulas)
  : ((X.1.prebox ∪ X.1.prebox.modalBox) ∪ {□ψ, -ψ}) ⊆ φ.subformulas⁻ := by
  intro χ hr;
  replace hr : χ = □ψ ∨ χ = -ψ ∨ □χ ∈ X ∨ (∃ a, □a ∈ X ∧ □a = χ) := by simp at hr; tauto;
  rcases hr with (rfl | rfl | hp | ⟨χ, hr, rfl⟩);
  · apply Finset.mem_union.mpr;
    tauto;
  · apply Finset.mem_union.mpr;
    right;
    apply Finset.mem_image.mpr;
    use ψ;
    constructor;
    · exact subformulas.mem_box hq;
    · tauto;
  · have := X.closed.subset hp;
    have := FormulaFinset.complementary_mem_box (by apply subformulas.mem_imp₁) this;
    apply Finset.mem_union.mpr;
    left;
    exact subformulas.mem_box this;
  · exact X.closed.subset hr;


lemma truthlemma_lemma2
  {X : ComplementClosedConsistentFinset Hilbert.GL φ.subformulas}
  (hq₁ : □ψ ∈ φ.subformulas)
  (hq₂ : □ψ ∉ X)
  : FormulaFinset.Consistent Hilbert.GL ((X.1.prebox ∪ X.1.prebox.modalBox) ∪ {□ψ, -ψ}) := by
  apply FormulaFinset.intro_union_consistent;
  rintro Γ₁ Γ₂ ⟨hΓ₁, hΓ₂⟩;
  replace hΓ₂ : ∀ χ ∈ Γ₂, χ = □ψ ∨ χ = -ψ := by
    simp_all
  by_contra hC;
  have : Γ₁ ⊢[_]! ⋀Γ₂ ==> ⊥ := provable_iff.mpr <| and_imply_iff_imply_imply'!.mp hC;
  have : Γ₁ ⊢[_]! (□ψ ⋏ -ψ) ==> ⊥ := imp_trans''! (by
    suffices Γ₁ ⊢[Hilbert.GL]! ⋀[□ψ, -ψ] ==> ⋀Γ₂ by
      simp_all
    apply conjconj_subset!;
    simpa using hΓ₂;
  ) this;
  have : Γ₁ ⊢[_]! □ψ ==> -ψ ==> ⊥ := and_imply_iff_imply_imply'!.mp this;
  have : Γ₁ ⊢[Hilbert.GL]! □ψ ==> ψ := by
    rcases Formula.complement.or (φ := ψ) with (hp | ⟨ψ, rfl⟩);
    · rw [hp] at this;
      exact imp_trans''! this dne!;
    · exact this;
  have : (□'Γ₁) ⊢[_]! □(□ψ ==> ψ) := contextual_nec! this;
  have : (□'Γ₁) ⊢[_]! □ψ := axiomL! ⨀ this;
  have : _ ⊢! ⋀□'Γ₁ ==> □ψ := provable_iff.mp this;
  have : _ ⊢! ⋀□'(X.1.prebox ∪ X.1.prebox.modalBox |>.toList) ==> □ψ :=
    imp_trans''! (conjconj_subset! (by
    simp_all
  )) this;
  have : _ ⊢! ⋀□'(X.1.prebox.toList) ==> □ψ := imp_trans''! (conjconj_provable! (by
    suffices ∀ χ, (□χ ∈ X ∨ ∃ χ', □χ' ∈ X ∧ □χ' =
      χ) → (□'^[1](Finset.premultibox 1 X).toList) ⊢[Hilbert.GL]! □χ by simpa;
    rintro χ (hχ | ⟨χ, hχ, rfl⟩);
    · apply FiniteContext.by_axm!;
      simpa;
    · apply axiomFour'!;
      apply FiniteContext.by_axm!;
      simpa;
  )) this;
  have : X *⊢[Hilbert.GL]! □ψ := by
    apply Context.provable_iff.mpr;
    use □'X.1.prebox.toList;
    constructor;
    · simp;
    · assumption;
  have : □ψ ∈ X := membership_iff hq₁ |>.mpr this;
  contradiction;

lemma truthlemma {X : (miniCanonicalModel φ).World} (q_sub : ψ ∈ φ.subformulas) :
  Satisfies (miniCanonicalModel φ) X ψ ↔ ψ ∈ X := by
  induction ψ using Formula.rec' generalizing X with
  | hatom => simp [Satisfies];
  | hfalsum => simp [Satisfies];
  | himp ψ χ ihq ihr =>
    constructor;
    · contrapose;
      intro h;
      apply Satisfies.imp_def.not.mpr;
      push Not;
      constructor;
      · apply ihq (subformulas.mem_imp₁ q_sub) |>.mpr;
        exact iff_not_mem_imp q_sub (subformulas.mem_imp₁ q_sub) (subformulas.mem_imp₂ q_sub)
          |>.mp h |>.1;
      · apply ihr (subformulas.mem_imp₂ q_sub) |>.not.mpr;
        have :=
          iff_not_mem_imp q_sub (subformulas.mem_imp₁ q_sub) (subformulas.mem_imp₂ q_sub)
            |>.mp h |>.2;
        exact iff_mem_compl (subformulas.mem_imp₂ q_sub) |>.not.mpr (by simpa using this);
    · contrapose;
      intro h;
      replace h := Satisfies.imp_def.not.mp h; push Not at h;
      obtain ⟨hq, hr⟩ := h;
      replace hq : ψ ∈ X := ihq (subformulas.mem_imp₁ q_sub) |>.mp hq;
      replace hr : χ ∉ X := ihr (subformulas.mem_imp₂ q_sub) |>.not.mp hr;
      apply iff_not_mem_imp q_sub (subformulas.mem_imp₁ q_sub) (subformulas.mem_imp₂ q_sub) |>.mpr;
      constructor;
      · assumption;
      · simpa using iff_mem_compl (subformulas.mem_imp₂ q_sub) |>.not.mp (by simpa using hr);
  | hbox ψ ih =>
    constructor;
    · contrapose;
      intro h;
      obtain ⟨Y, hY₁⟩ :=
        lindenbaum (Ψ := φ.subformulas) (truthlemma_lemma1 q_sub) (truthlemma_lemma2 q_sub h);
      simp only [Finset.union_subset_iff] at hY₁;
      apply Satisfies.box_def.not.mpr;
      push Not;
      use Y;
      constructor;
      · constructor;
        · aesop;
        · aesop;
      · apply ih ?_ |>.not.mpr;
        · apply iff_mem_compl (subformulas.mem_box q_sub) |>.not.mpr;
          push Not;
          apply hY₁.2;
          simp;
        · exact subformulas.mem_box q_sub;
    · intro h Y RXY;
      apply ih (subformulas.mem_box q_sub) |>.mpr;
      refine RXY.1 ψ ?_ h |>.1;
      assumption;

instance finiteComplete : Complete Hilbert.GL Kripke.TransitiveIrreflexiveFiniteFrameClass := ⟨by
  intro φ;
  contrapose;
  intro h;
  apply ValidOnFiniteFrameClass.not_of_exists_frame;
  use (miniCanonicalFrame φ);
  constructor;
  · exact ⟨miniCanonicalFrame.is_transitive, miniCanonicalFrame.is_irreflexive⟩;
  · apply ValidOnFrame.not_of_exists_model_world;
    obtain ⟨X, hX₁⟩ := lindenbaum (Φ := {-φ}) (Ψ := φ.subformulas)
      (by
        simp only [FormulaFinset.complementary, Finset.singleton_subset_iff, Finset.mem_union,
          Finset.mem_image];
        right;
        use φ;
        constructor <;> simp;
      )
      (FormulaFinset.unprovable_iff_singleton_compl_consistent.mpr h);
    use (miniCanonicalModel φ), X;
    constructor;
    · tauto;
    · apply truthlemma (by simp) |>.not.mpr;
      exact iff_mem_compl (by simp) |>.not.mpr <| by
        push Not;
        apply hX₁;
        tauto;
⟩

end Kripke
end GL
end Hilbert
end Modal
end LO
